plugins {
    id 'java'
    id 'io.quarkus'
    id 'io.freefair.lombok' version '8.6'
}

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    mapstructVersion = "1.6.0"
    lombokMapstructBindingVersion = "0.2.0"
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-rest-jackson'
    implementation 'io.quarkus:quarkus-arc'
    implementation 'io.quarkus:quarkus-hibernate-validator'
    implementation 'io.quarkus:quarkus-hibernate-orm-panache'
    implementation 'io.quarkus:quarkus-jdbc-postgresql'
    implementation 'io.quarkus:quarkus-jdbc-h2'
    implementation 'io.quarkus:quarkus-hibernate-envers'
    implementation 'io.quarkus:quarkus-qute'
    implementation 'com.itextpdf:html2pdf:6.0.0'
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.quarkus:quarkus-junit5-mockito'
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
    testImplementation 'io.rest-assured:rest-assured'
}

lombok {
    version = "1.18.30"
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    jvmArgs '-Dquarkus.profile=test'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

tasks.withType(GenerateModuleMetadata).configureEach {
    suppressedValidationErrors.add('enforced-platform')
}

allprojects {
    tasks.withType(Test).configureEach {
        systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
        jvmArgs "-Dquarkus.application.version=${quarkusPlatformVersion}",
                '-Duser.timezone=America/Sao_Paulo',
                '-Duser.language=pt',
                '-Duser.country=BR',
                '-Dfile.encoding=UTF-8'
    }
}

tasks.withType(JavaExec).configureEach {
    jvmArgs "-Dquarkus.application.version=${quarkusPlatformVersion}",
            '-Duser.timezone=America/Sao_Paulo',
            '-Duser.language=pt',
            '-Duser.country=BR',
            '-Dfile.encoding=UTF-8'
}
